 /*   SIST ENDRET PÅ PROD   2001.09.13 11.56.43 AV   HLA2970          */        
 /*       SIST ENDRET 07/05-98 15.57.35 AV   SPA7339                  */        
 /*       SIST ENDRET 09/03-98 10.33.38 AV   SPA7339                  */        
  /* **************************************************************** */        
  /*IDENTIFIKASJON:                                                   */        
  /*    R0015501 - HOVEDPROGRAM I PLI                                 */        
  /*    PROGRAMMERER: LUNDEBY JUNI     1982                           */        
  /*                                                                  */        
  /*HENSIKT:                                                          */        
  /*    FJERNER EN ELLER FLERE TRANSER UNDER EN PERSON                */        
  /*                                                                  */        
  /*PROGRAMTILKNYTNING:                                               */        
  /*                                                                  */        
  /*BRUK:                                                             */        
  /*                                                                  */        
  /* **************************************************************** */        
                                                                                
  R001550 :                                                                     
    PROC            (LOKAL_KOM_PTR)                                             
           OPTIONS  (MAIN);                                                     
                                                                                
  %PAGE;                                                                        
  %INCLUDE P0019908;                      /*   KOM_OMR        (B) */            
                                                                                
  %PAGE;                                                                        
  %INCLUDE P0019912;                      /*   DIV_PARAM_OMR  (B) */            
                                                                                
  %PAGE;                                                                        
  %INCLUDE P0019913;                      /*   LOKAL_KOM_OMR  (B) */            
                                                                                
  %PAGE;                                                                        
  /* **************************************************************** */        
  /*     INNEBYGDE PLI-FUNKSJONER  + PEKER-DEF.                       */        
  /* **************************************************************** */        
                                                                                
     DCL                                                                        
       ( ADDR , LOW , CSTG )                BUILTIN;                            
                                                                                
     DCL                                                                        
       PLITDLI ENTRY;                                                           
                                                                                
     DCL                                                                        
       ( LOKAL_KOM_PTR , COMMAREA_PEKER , W001_HJELPE_PEKER )  POINTER;         
                                                                                
                                                                                
  /* **************************************************************** */        
  /*     ARBEIDSFELTER SOM BRUKES AV UIB OG PCB  .                    */        
  /* **************************************************************** */        
                                                                                
  %INCLUDE DLIUIB;                                                              
                                                                                
     DCL                                                                        
         UIB_RC_OK                 BIT (8)       INIT( 0 );                     
                                                                                
  %INCLUDE  P0012002;                  /* UIB_PCB_PEKER_OMR (BASED)   */        
                                                                                
     DCL  1 RF1                    BASED (RF1_PCB_PEKER),                       
  %INCLUDE  P0012003;                  /* PCB    (BASED)             */         
                                                                                
     DCL  1 RF2                    BASED (RF2_PCB_PEKER),                       
  %INCLUDE  P0012003;                  /* PCB    (BASED)             */         
                                                                                
     DCL  1 RF3                    BASED (RF3_PCB_PEKER),                       
  %INCLUDE  P0012003;                  /* PCB    (BASED)             */         
                                                                                
  /* **************************************************************** */        
  /*     OPPTELLINGS- OG LOOP-PARAMETRE M.M                           */        
  /* **************************************************************** */        
                                                                                
     DCL 1  W001_OPPTELL_OMR,                                                   
            2   I                  FIXED    BIN   (15)   INIT ( 0  ),           
            2   ROT_SKAL_FJERNES            BIT   (1 )   INIT ('0'B),           
            2   MER_DATA                    BIT   (1 )   INIT ('1'B),           
            2   B                           CHAR  (1)    INIT (' ' );           
                                                                                
                                                                                
    /* ************************************************************ */          
    /* TILKNYTTNINGS-SEGM .  BRUKES I 1. RF-PCB.   (RF1)            */          
    /* ************************************************************ */          
                                                                                
     DCL 1  W01_TILKN1             UNALIGNED,                                   
     %INCLUDE  P0019931;                     /*   TILKN-SEGM        */          
                                                                                
    /* ************************************************************ */          
    /* TILKNYTTNINGS-SEGM .  BRUKES I 2. RF-PCB.  (RF2)             */          
    /* ************************************************************ */          
                                                                                
     DCL 1  W01_TILKN2             UNALIGNED,                                   
     %INCLUDE  P0019931;                     /*   TILKN-SEGM        */          
                                                                                
                                                                                
    /* ************************************************************ */          
    /* TILBERPO-SEGM.BRUKES I OPPDATERING AV TILBERPO-SEGM.    */               
    /* ************************************************************ */          
                                                                                
     DCL 1  W01_TILBERPO           UNALIGNED,                                   
     %INCLUDE  P0019929;                     /*   TILBERPO-SEGM.    */          
                                                                                
                                                                                
  /* **************************************************************** */        
  /*     DLI CALL-PARAMETRE                                           */        
  /* **************************************************************** */        
                                                                                
     DCL 1  W001_DLI               UNALIGNED,                                   
            2   GET                CHAR     (4)   INIT ('    '),                
            2   GHU                CHAR     (4)   INIT ('GHU '),                
            2   GNP                CHAR     (4)   INIT ('GNP '),                
            2   GHNP               CHAR     (4)   INIT ('GHNP'),                
            2   DLET               CHAR     (4)   INIT ('DLET'),                
            2   FUNC               CHAR     (4)   INIT ('    '),                
            2   PARM_CT_3          FIXED BIN(31)  INIT (3),                     
            2   PARM_CT_4          FIXED BIN(31)  INIT (4),                     
            2   PARM_CT_5          FIXED BIN(31)  INIT (5),                     
            2   PARM_CT_6          FIXED BIN(31)  INIT (6),                     
            2   OUT2               CHAR     (20)  INIT ('');                    
                                                                                
     DCL  1 W001_IO      UNALIGNED                            ,                 
            2   FILLER1            CHAR     ( 31) INIT  ('')  ,                 
            2   RF0PERSN_TKNR      DEC FIXED  (5) INIT  ('')  ,                 
            2   FILLER2            CHAR     ( 04) INIT  ('')  ,                 
            2   RF0PERSN_SPERRE    CHAR     (  1) INIT  ('')  ,                 
            2   FILLER3            CHAR     (111) INIT  ('')  ;                 
                                                                                
     DCL    W001_HJELPE_IO         CHAR (150) BASED (W001_HJELPE_PEKER);        
                                                                                
  %PAGE;                                                                        
  /* **************************************************************** */        
  /*     DL1 SSA-OMRÅDER FOR ROT OG STATUS-SEGMENTER.                 */        
  /* **************************************************************** */        
                                                                                
     DCL    SSA_UQUAL                            CHAR (9)  INIT ('');           
                                                                                
     DCL 1  SSA1_RF0PERSN          UNALIGNED,                                   
            2    HDEL                            CHAR (17) INIT                 
                 ('RF0PERSN(FNR     '),                                         
            2    REL_OP                          CHAR (2)  INIT (' ='),         
            2    PKEY               FIXED        DEC  (11) INIT ( 0 ),          
            2    HP                              CHAR (1)  INIT (')');          
                                                                                
     DCL 1  SSA2_RF0PERSN          UNALIGNED,                                   
            2    HDEL                            CHAR (17) INIT                 
                 ('RF0PERSN(FNR     '),                                         
            2    REL_OP                          CHAR (2)  INIT (' ='),         
            2    PKEY               FIXED        DEC  (11) INIT ( 0 ),          
            2    HP                              CHAR (1)  INIT (')');          
                                                                                
     DCL 1  SSA3_RF0PERSN          UNALIGNED,                                   
            2    HDEL                            CHAR (17) INIT                 
                 ('RF0PERSN(FNR     '),                                         
            2    REL_OP                          CHAR (2)  INIT (' ='),         
            2    PKEY               FIXED        DEC  (11) INIT ( 0 ),          
            2    HP                              CHAR (1)  INIT (')');          
                                                                                
     DCL 1  SSA1_PINNTEKT          UNALIGNED,                                   
            2    HDEL                            CHAR (17) INIT                 
                 ('PINNTEKT(INNTTYPE'),                                         
            2    REL_OP                          CHAR (2)  INIT (' ='),         
            2    TYPE                            CHAR (1)  INIT ('A' ),         
            2    HP                              CHAR (1)  INIT (')' );         
                                                                                
     DCL 1  SSA2_PINNTEKT          UNALIGNED     CHAR (11) INIT                 
               ('PINNTEKT*F ');                                                 
                                                                                
                                                                                
     DCL 1  SSA1_STATUS            UNALIGNED,                                   
            2    HDEL                            CHAR (17) INIT                 
                 ('STATUS  (STATKODE'),                                         
            2    REL_OP                          CHAR (2)  INIT (' ='),         
            2    KODE                            CHAR (1)  INIT (' ' ),         
            2    HP                              CHAR (1)  INIT (')' );         
                                                                                
     DCL 1  SSA1_TILKN             UNALIGNED,                                   
            2    HDEL                            CHAR (17) INIT                 
                 ('TILKN   (FNRANDR '),                                         
            2    REL_OP                          CHAR (2)  INIT (' ='),         
            2    PKEY               FIXED        DEC  (11) INIT ( 0 ),          
            2    HP                              CHAR (1)  INIT (')');          
                                                                                
                                                                                
                                                                                
  %PAGE;                                                                        
  /* **************************************************************** */        
  /*                                                                  */        
  /*        SLUTT PÅ DEKLARASJONENE , EKSEKVERINGEN STARTER .         */        
  /*       ---------------------------------------------------        */        
  /*                                                                  */        
  /*                                                                  */        
  /*        INITIERER VISSE DEFAULTVERDIER.                           */        
  /*                                                                  */        
  /* **************************************************************** */        
                                                                                
                                                                                
    COMMAREA_PEKER      =    HOVED_KOM_OMR_PTR ;                                
    IF FEIL_MELD_NR = 0 THEN /* AB 01.08.16 */                                  
      PROGRAM_ID          =   'R0015501'         ;                              
      UIBPTR              =    UIB_PEKER         ;                              
                                                                                
    IF FJERN_ALLE                                THEN                           
       DO;                                                                      
          /* ******************************************************** */        
          /* GÅR IGJENNOM TILKNYTTINGENE TIL HOVEDPERSON FOR Å FJERNE.*/        
          /*                                                          */        
          /*  HVIS FJERN ALLE :                                       */        
          /*                                                          */        
          /*       SØKER                                              */        
          /*       SØKERS TILKNYTTNINGER                              */        
          /*       TILKNYTTNINGENE  TIL SØKERS TILKNYTTNINGER         */        
          /*                                                          */        
          /* NEST SISTE OG SISTE STATUS . I TILLEGG FJERNES OGSÅ      */        
          /*                      TILBERPO OG TRANHIST.               */        
          /* FORELØPIGE INNTEKTER SKAL IFLG. HULTGREEN IKKE FJERNES   */        
          /*                                                          */        
          /*  HVIS IKKE       FJERNES BARE SISTE TRANHIST             */        
          /* ******************************************************** */        
                                                                                
          SSA1_RF0PERSN.PKEY                    =    SEARCH_FNR        ;        
                                                                                
          SSA1_STATUS.KODE                      =   'S'                ;        
          CALL P030_LEST_TILKN_FJERNET_ALT                             ;        
                                                                                
          IF FEIL_MELD_NR    >  0      THEN                                     
             GO   TO   L999;                                                    
                                                                                
                                                                                
          SSA1_STATUS.KODE                      =   'N'                ;        
          CALL P030_LEST_TILKN_FJERNET_ALT                             ;        
                                                                                
          IF FEIL_MELD_NR    >  0      THEN                                     
             GO   TO   L999;                                                    
                                                                                
       END;     /*   IF   FJERN_ALLE                   */                       
    ELSE                                                                        
       DO;                                                                      
          /* ****************************************************** */          
          /* FJERNER SISTE :     TRANHIST                           */          
          /* ****************************************************** */          
                                                                                
          SSA_UQUAL                           =   'TRANHIST '        ;          
          W001_DLI.GET                        =   'GU  '             ;          
          SSA3_RF0PERSN.PKEY                  =    SEARCH_FNR        ;          
          CALL P005_LES_RF0PERSN;                                               
                                                                                
          IF FEIL_MELD_NR                     >    0   THEN                     
             GO   TO   L999;                                                    
                                                                                
          CALL P014_FJERN_SEGM    ;                                             
                                                                                
          IF FEIL_MELD_NR    >  0  THEN                                         
             GO   TO   L999;                                                    
       END;                                                                     
                                                                                
                                                                                
                                                                                
    /* ************************************************************** */        
    /* ************************************************************** */        
    /* ********                                        ************** */        
    /* ********    I N T E R N E  P R O C E D U R E R  ************** *         
    /* ********                                        ************** */        
    /* ************************************************************** */        
    /* ************************************************************** */        
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å LESE    : RF0PERSN                             */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P005_LES_RF0PERSN :                                                         
      PROC ;                                                                    
                                                                                
                                                                                
                                                                                
      CALL PLITDLI                          (W001_DLI.PARM_CT_4,                
                                             W001_DLI.GET,                      
                                             RF3,                               
                                             W001_IO,                           
                                             SSA3_RF0PERSN);                    
                                                                                
      IF  DLIUIB.UIBRCODE.UIBFCTR   ^=       UIB_RC_OK    THEN                  
        DO;                                                                     
          /* ******************************************** */                    
          /* FEIL VED LES   AV :  RF0PERSN                */                    
          /* ******************************************** */                    
  L100:                                                                         
          FEIL_MELD_NR               =       500 ;                              
          FEIL_VED_LABEL             =      '100';                              
          GO   TO  RETUR;                                                       
        END;                                                                    
                                                                                
      SELECT  ( RF3.STATUS_KODE );                                              
                                                                                
         WHEN ('  ')                                                            
                                                                                
              /* ******************************************** */                
              /* RF0PERSN   FUNNET                            */                
              /* ******************************************** */                
                                                                                
              MER_DATA   =  '1'B ;                                              
                                                                                
         WHEN ('GE')                                                            
                                                                                
              /* ******************************************** */                
              /* RF0PERSN   IKKE FUNNET                       */                
              /* ******************************************** */                
                                                                                
              MER_DATA   =  '0'B ;                                              
                                                                                
         OTHERWISE                                                              
            DO;                                                                 
              /* ******************************************** */                
              /* FEIL VED LES   AV :  RF0PERSN                */                
              /* ******************************************** */                
  L110:                                                                         
              FEIL_MELD_NR             =       500 ;                            
              FEIL_VED_LABEL           =      '110';                            
              DB_STATUS_KODE           =       RF3.STATUS_KODE;                 
              GO   TO   RETUR;                                                  
            END;                                                                
                                                                                
      END;/*   SELECT                               */                          
                                                                                
  RETUR:                                                                        
    END P005_LES_RF0PERSN;                                                      
                                                                                
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å SJEKKE OM :   UNDERLIGGENDE SEGMENTER FINNES   */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P006_SJEKK_OM_UNDERSEGM_FINNES:                                             
      PROC ;                                                                    
                                                                                
                                                                                
                                                                                
      W001_DLI.GET                   =      'GU  ';                             
      CALL P005_LES_RF0PERSN;                                                   
                                                                                
      IF FEIL_MELD_NR                >       0          THEN                    
         GO   TO   RETUR;                                                       
                                                                                
      CALL PLITDLI                          (W001_DLI.PARM_CT_3,                
                                             W001_DLI.GNP,                      
                                             RF3,                               
                                             W001_IO);                          
                                                                                
      IF  DLIUIB.UIBRCODE.UIBFCTR ^=       UIB_RC_OK    THEN                    
        DO;                                                                     
          /* ******************************************** */                    
          /* FEIL VED DB                                  */                    
          /* ******************************************** */                    
  L120:                                                                         
          FEIL_MELD_NR             =       500 ;                                
          FEIL_VED_LABEL           =      '120';                                
          GO   TO  RETUR;                                                       
        END;                                                                    
                                                                                
                                                                                
      SELECT  ( RF3.STATUS_KODE );                                              
                                                                                
         WHEN ('  ')                                                            
                                                                                
              /* *************************************************** */         
              /* UNDERLIGGENDE SEGM FINNES. ROT SKAL IKKE FJERNES    */         
              /* *************************************************** */         
                                                                                
              ROT_SKAL_FJERNES         =     '0'B ;                             
                                                                                
         WHEN ('GE')                                                            
                                                                                
              /* *************************************************** */         
              /* UNDERLIGGENDE SEGM FINNES IKKE. ROT SKAL FJERNES    */         
              /* *************************************************** */         
                                                                                
    /*        ROT_SKAL_FJERNES         =      '1'B ;  SP 15.10.92 */            
              ROT_SKAL_FJERNES         =      '0'B ;                            
                                                                                
         OTHERWISE                                                              
            DO;                                                                 
              /* ******************************************** */                
              /* FEIL VED LES  GNP                            */                
              /* ******************************************** */                
  L125:                                                                         
              FEIL_MELD_NR             =       500 ;                            
              FEIL_VED_LABEL           =      '125';                            
              DB_STATUS_KODE           =       RF3.STATUS_KODE;                 
              GO   TO  RETUR;                                                   
            END;                                                                
                                                                                
      END;/*   SELECT                               */                          
                                                                                
                                                                                
  RETUR:                                                                        
                                                                                
    END P006_SJEKK_OM_UNDERSEGM_FINNES;                                         
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å FJERNE / REPLACE :  RF0PERSN                   */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P008_FJERN_REPL_ROT :                                                       
      PROC ;                                                                    
                                                                                
                                                                                
                                                                                
      W001_DLI.GET                   =      'GHU ';                             
      CALL P005_LES_RF0PERSN;                                                   
                                                                                
       IF FJERN_ALLE      THEN                                                  
          RF0PERSN_TKNR   =     0     ;                                         
      IF FEIL_MELD_NR                >       0          THEN                    
         GO   TO   RETUR;                                                       
                                                                                
      CALL PLITDLI                          (W001_DLI.PARM_CT_3,                
                                             W001_DLI.FUNC,                     
                                             RF3,                               
                                             W001_IO);                          
                                                                                
      IF DLIUIB.UIBRCODE.UIBFCTR    ^=       UIB_RC_OK    !                     
         RF3.STATUS_KODE            ^=       '  '         THEN                  
        DO;                                                                     
          /* ******************************************** */                    
          /* FEIL VED FJERN / REPL  :  RF0PERSN           */                    
          /* ******************************************** */                    
  L130:                                                                         
          FEIL_MELD_NR               =       500 ;                              
          FEIL_VED_LABEL             =      '130';                              
          DB_STATUS_KODE             =       RF3.STATUS_KODE;                   
          GO   TO  RETUR;                                                       
        END;                                                                    
                                                                                
  RETUR:                                                                        
    END P008_FJERN_REPL_ROT;                                                    
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å FJERNE FORELØPIG PINNTEKT, TILBERPO, TRANHIST  */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P010_FJERN_RESTEN :                                                         
      PROC ;                                                                    
                                                                                
                                                                                
                                                                                
      W001_DLI.GET              =   'GU  ';                                     
      CALL P005_LES_RF0PERSN;                                                   
                                                                                
      IF FEIL_MELD_NR           >    0    THEN                                  
         GO   TO   RETUR;                                                       
                                                                                
       IF MER_DATA                        THEN                                  
          DO;                                                                   
             /* *********************************************** */              
             /* FJERNER ALLE  FORELØPIGE PINNTEKT               */              
             /* *********************************************** */              
             /* *********************************************** */              
             /* SKAL IKKE FJERNES IFLG HULTGREEN 6.2.2001       */              
             /* *********************************************** */              
                                                                                
   /*        DO      WHILE ( MER_DATA );                                        
                CALL P012_FJERN_FORELØPIG_PINNTEKT;                             
                                                                                
                IF   FEIL_MELD_NR          >      0    THEN                     
                     GO   TO   RETUR;                                           
             END;                                                               
   */                                                                           
                                                                                
             /* *********************************************** */              
             /* OPPDATERER TILBERPO-SEGMENTET.             */                   
             /* *********************************************** */              
                                                                                
                                                                                
             SSA_UQUAL  =  'TILBERPO '                            ;             
                                                                                
             CALL  P013_LES_TILBERPO                              ;             
                                                                                
             IF FEIL_MELD_NR    >  0  THEN                                      
                GO   TO   RETUR                                   ;             
             ELSE                                                               
                IF MER_DATA THEN                                                
                   DO;                                                          
                      CALL  P015_REPL_TILBERPO                    ;             
                                                                                
                      IF FEIL_MELD_NR    >  0  THEN                             
                         GO   TO   RETUR                          ;             
                   END;                                                         
                                                                                
             /* ************************************************ */             
             /* FJERNER ALLE            TRANHIST                 */             
             /* ************************************************ */             
                                                                                
             MER_DATA   =  '1'B                                   ;             
             SSA_UQUAL  =  'TRANHIST '                            ;             
                                                                                
             DO WHILE ( MER_DATA )                                ;             
                CALL  P014_FJERN_SEGM                             ;             
                                                                                
                IF FEIL_MELD_NR    >  0  THEN                                   
                   GO   TO   RETUR                                ;             
             END;                                                               
                                                                                
             /* *********************************************** */              
             /* SJEKKER OM ROT-SEGM SKAL FJERNES                */              
             /* HVIS IKKE ÅMD SPERRE_KODEN BLANKES .            */              
             /* *********************************************** */              
                                                                                
             CALL P006_SJEKK_OM_UNDERSEGM_FINNES;                               
                                                                                
             IF FEIL_MELD_NR               >      0    THEN                     
                GO   TO   RETUR;                                                
                                                                                
             IF ROT_SKAL_FJERNES                       THEN                     
                DO;                                                             
                   W001_DLI.FUNC           =     'DLET';                        
                   CALL P008_FJERN_REPL_ROT;                                    
                END;                                                            
             ELSE                                                               
                DO;                                                             
                   W001_DLI.FUNC           =     'REPL';                        
                   RF0PERSN_SPERRE         =     ' '   ;                        
                   CALL P008_FJERN_REPL_ROT;                                    
                END;                                                            
          END;                                                                  
                                                                                
  RETUR:                                                                        
    END P010_FJERN_RESTEN;                                                      
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å FJERNE    FORELØPIG  PINNTEKT                  */        
    /*                                                                */        
    /* RUTINA UTGÅR 6.2.01 - ØNSKE FRA HULTGREEN                      */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P012_FJERN_FORELØPIG_PINNTEKT:                                              
      PROC ;                                                                    
                                                                                
                                                                                
                                                                                
      CALL PLITDLI                          (W001_DLI.PARM_CT_4,                
                                             W001_DLI.GHNP,                     
                                             RF3,                               
                                             W001_IO,                           
                                             SSA1_PINNTEKT);                    
                                                                                
      IF  DLIUIB.UIBRCODE.UIBFCTR ^=       UIB_RC_OK    THEN                    
        DO;                                                                     
          /* ******************************************** */                    
          /* FEIL VED LES   AV :  PINNTEKT                */                    
          /* ******************************************** */                    
  L140:                                                                         
          FEIL_MELD_NR             =       500 ;                                
          FEIL_VED_LABEL           =      '140';                                
          GO   TO  RETUR;                                                       
        END;                                                                    
                                                                                
                                                                                
      SELECT  ( RF3.STATUS_KODE );                                              
                                                                                
         WHEN ('  ')                                                            
            DO;                                                                 
              /* ******************************************** */                
              /* PINNTEKT   FUNNET . SEGMENTET FJERNES        */                
              /* ******************************************** */                
                                                                                
              MER_DATA             =         '1'B ;                             
                                                                                
              CALL PLITDLI                   (W001_DLI.PARM_CT_3,               
                                              W001_DLI.DLET,                    
                                              RF3,                              
                                              W001_IO);                         
                                                                                
              IF  DLIUIB.UIBRCODE.UIBFCTR ^=  UIB_RC_OK                         
                ! RF3.STATUS_KODE         ^=  '  '         THEN                 
                DO;                                                             
                  /* ******************************************** */            
                  /* FEIL VED DELETE  AV :  PINNTEKT              */            
                  /* ******************************************** */            
  L150:                                                                         
                  FEIL_MELD_NR             =   500 ;                            
                  FEIL_VED_LABEL           =  '150';                            
                  DB_STATUS_KODE           =   RF3.STATUS_KODE;                 
                  MER_DATA                 =   '0'B ;                           
                  GO   TO  RETUR;                                               
                END;                                                            
                                                                                
            END;                                                                
                                                                                
         WHEN ('GE')                                                            
            DO;                                                                 
              /* ******************************************** */                
              /* PINNTEKT   IKKE FUNNET                       */                
              /* ******************************************** */                
                                                                                
              MER_DATA                 =      '0'B ;                            
            END;                                                                
                                                                                
         OTHERWISE                                                              
            DO;                                                                 
              /* ******************************************** */                
              /* FEIL VED LES   AV :  PINNTEKT                */                
              /* ******************************************** */                
  L160:                                                                         
              FEIL_MELD_NR             =       500 ;                            
              FEIL_VED_LABEL           =      '160';                            
              DB_STATUS_KODE           =       RF3.STATUS_KODE;                 
              GO   TO  RETUR;                                                   
            END;                                                                
                                                                                
      END;/*   SELECT                               */                          
                                                                                
                                                                                
  RETUR:                                                                        
                                                                                
    END P012_FJERN_FORELØPIG_PINNTEKT;                                          
                                                                                
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å LESE TILBERPO.                                 */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
                                                                                
    P013_LES_TILBERPO:                                                          
      PROC ;                                                                    
                                                                                
                                                                                
                                                                                
      CALL PLITDLI                          (W001_DLI.PARM_CT_4,                
                                             W001_DLI.GHNP,                     
                                             RF3,                               
                                             W01_TILBERPO,                      
                                             SSA_UQUAL);                        
                                                                                
      IF  DLIUIB.UIBRCODE.UIBFCTR   ^=       UIB_RC_OK    THEN                  
        DO;                                                                     
          /* ******************************************** */                    
          /* FEIL VED LES   AV :  TILBERPO                */                    
          /* ******************************************** */                    
  L162:                                                                         
          FEIL_MELD_NR               =       500 ;                              
          FEIL_VED_LABEL             =      '162';                              
          GO   TO  RETUR;                                                       
        END;                                                                    
                                                                                
      SELECT  ( RF3.STATUS_KODE );                                              
                                                                                
         WHEN ('  ')                                                            
                                                                                
              /* ******************************************** */                
              /* TILBERPO   FUNNET                            */                
              /* ******************************************** */                
                                                                                
              MER_DATA   =  '1'B ;                                              
                                                                                
         WHEN ('GE')                                                            
                                                                                
              /* ******************************************** */                
              /* TILBERPO   IKKE FUNNET                       */                
              /* ******************************************** */                
                                                                                
              MER_DATA   =  '0'B ;                                              
                                                                                
         OTHERWISE                                                              
            DO;                                                                 
              /* ******************************************** */                
              /* FEIL VED LES   AV :  TILBERPO                */                
              /* ******************************************** */                
  L164:                                                                         
              FEIL_MELD_NR             =       500 ;                            
              FEIL_VED_LABEL           =      '164';                            
              DB_STATUS_KODE           =       RF3.STATUS_KODE;                 
              GO   TO   RETUR;                                                  
            END;                                                                
                                                                                
      END;/*   SELECT                               */                          
                                                                                
  RETUR:                                                                        
    END P013_LES_TILBERPO;                                                      
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å FJERNE ET SEGM UNDER RF0PERSN                  */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P014_FJERN_SEGM :                                                           
      PROC ;                                                                    
                                                                                
                                                                                
                                                                                
      CALL PLITDLI                          (W001_DLI.PARM_CT_4,                
                                             W001_DLI.GHNP,                     
                                             RF3,                               
                                             W001_IO,                           
                                             SSA_UQUAL);                        
                                                                                
      IF  DLIUIB.UIBRCODE.UIBFCTR ^=       UIB_RC_OK    THEN                    
        DO;                                                                     
          /* ******************************************** */                    
          /* FEIL PÅ DB                                   */                    
          /* ******************************************** */                    
  L170:                                                                         
          FEIL_MELD_NR             =       500 ;                                
          FEIL_VED_LABEL           =      '170';                                
          GO   TO  RETUR;                                                       
        END;                                                                    
                                                                                
                                                                                
      SELECT  ( RF3.STATUS_KODE );                                              
                                                                                
         WHEN ('  ')                                                            
            DO;                                                                 
              /* ******************************************** */                
              /* ØNSKET SEGM FUNNET  OG KAN FJERNES           */                
              /* ******************************************** */                
                                                                                
              MER_DATA                 =      '1'B ;                            
                                                                                
              CALL PLITDLI                   (W001_DLI.PARM_CT_3,               
                                              W001_DLI.DLET,                    
                                              RF3,                              
                                              W001_IO);                         
                                                                                
              IF  DLIUIB.UIBRCODE.UIBFCTR ^=  UIB_RC_OK                         
                ! RF3.STATUS_KODE         ^=  '  '         THEN                 
                DO;                                                             
                  /* ******************************************** */            
                  /* FEIL VED DELETE  ØNSKET SEGM                 */            
                  /* ******************************************** */            
  L180:                                                                         
                  FEIL_MELD_NR             =   500 ;                            
                  FEIL_VED_LABEL           =  '180';                            
                  DB_STATUS_KODE           =   RF3.STATUS_KODE;                 
                  MER_DATA                 =  '0'B ;                            
                  GO   TO  RETUR;                                               
                END;                                                            
            END;                                                                
                                                                                
         WHEN ('GE')                                                            
                                                                                
              /* ************************************************* */           
              /* ØNSKET SEGM FINNES IKKE. RETUR_KODE ER OK         */           
              /* ************************************************* */           
                                                                                
              MER_DATA                 =      '0'B ;                            
                                                                                
         OTHERWISE                                                              
            DO;                                                                 
              /* ******************************************** */                
              /* FEIL VED LES   ØNSKET SEGM                   */                
              /* ******************************************** */                
  L190:                                                                         
              FEIL_MELD_NR             =       500 ;                            
              FEIL_VED_LABEL           =      '190';                            
              DB_STATUS_KODE           =       RF3.STATUS_KODE;                 
              GO   TO  RETUR;                                                   
            END;                                                                
                                                                                
      END;/*   SELECT                               */                          
                                                                                
  RETUR:                                                                        
                                                                                
    END P014_FJERN_SEGM;                                                        
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å FJERNE  VERNEPLIKTSÅR I TILBERPO.              */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P015_REPL_TILBERPO:                                                         
      PROC ;                                                                    
                                                                                
                                                                                
      W01_TILBERPO.VERNEPLIKTÅR(1) = 0                       ;                  
      W01_TILBERPO.VERNEPLIKTÅR(2) = 0                       ;                  
      W01_TILBERPO.VERNEPLIKTÅR(3) = 0                       ;                  
      W01_TILBERPO.VERNEPLIKTÅR(4) = 0                       ;                  
                                                                                
      W001_DLI.FUNC = 'REPL '                                ;                  
                                                                                
      CALL PLITDLI                          (W001_DLI.PARM_CT_3,                
                                             W001_DLI.FUNC,                     
                                             RF3,                               
                                             W01_TILBERPO);                     
                                                                                
      IF DLIUIB.UIBRCODE.UIBFCTR    ^=       UIB_RC_OK    !                     
         RF3.STATUS_KODE            ^=       '  '         THEN                  
        DO;                                                                     
          /* ******************************************** */                    
          /* FEIL VED REPL AV TILBERPO                    */                    
          /* ******************************************** */                    
  L192:                                                                         
          FEIL_MELD_NR               =       500 ;                              
          FEIL_VED_LABEL             =      '192';                              
          DB_STATUS_KODE             =       RF3.STATUS_KODE;                   
          GO   TO  RETUR;                                                       
        END;                                                                    
                                                                                
  RETUR:                                                                        
                                                                                
                                                                                
    END P015_REPL_TILBERPO;                                                     
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å FJERNE    SISTE / NEST SISTE   STATUS FOR :    */        
    /*                                                                */        
    /*       SØKER                                                    */        
    /*       SØKERS TILKNYTTNINGER                                    */        
    /*       TILKNYTTNINGENE  TIL SØKERS TILKNYTTNINGER               */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P030_LEST_TILKN_FJERNET_ALT :                                               
      PROC;                                                                     
                                                                                
                                                                                
      SSA_UQUAL                       =   'TILKN    ';                          
      W001_DLI.GET                    =   'GU  '     ;                          
                                                                                
      /************************************************************** */        
      /*  LESER TILKNYTTNINGENE TIL SØKER.                            */        
      /************************************************************** */        
                                                                                
      DO WHILE   ( P034_LEST_TILKN_PCB1 );                                      
                                                                                
         SSA2_RF0PERSN.PKEY           =    W01_TILKN1.FNR_TILKN;                
         W001_DLI.GET                 =   'GU  '         ;                      
                                                                                
         /* ****************************************************** */           
         /* LESER TILKNYTTNINGENE TIL SØKER'S TILKNYTTNINGER.      */           
         /* FJERNER DERES STATUS_SEGM. HVIS DE ER FORSKJELLIG      */           
         /* FRA SØKER.                                             */           
         /* ****************************************************** */           
                                                                                
         IF W01_TILKN1.TILKNYTNINGSKODE      ^=    'D'       THEN               
            DO;                                                                 
               DO WHILE   ( P036_LEST_TILKN_PCB2 );                             
                                                                                
                  IF W01_TILKN2.FNR_TILKN    ^=  SSA1_RF0PERSN.PKEY THEN        
                     DO;                                                        
                        SSA3_RF0PERSN.PKEY    =  W01_TILKN2.FNR_TILKN;          
                                                                                
                        IF W01_TILKN2.TILKNYTNINGSKODE =  'D'  THEN             
                           DO;                                                  
                              SSA1_TILKN.REL_OP = '^='            ;             
                              SSA1_TILKN.PKEY   = W01_TILKN1.FNR_TILKN;         
                                                                                
                              IF P037_FLERE_TILKN                   THEN        
                                 DO;                                            
                                    SSA1_TILKN.REL_OP  =  ' ='      ;           
                                    CALL P038_FJERN_TILKN           ;           
                                 END;                                           
                              ELSE                                              
                                 DO;                                            
                                    CALL P032_FJERN_STATUS          ;           
                                                                                
                                    IF   FEIL_MELD_NR     >  0      THEN        
                                         GO   TO   RETUR;                       
                                                                                
                                    CALL P010_FJERN_RESTEN          ;           
                                 END;                                           
                           END;                                                 
                        ELSE                                                    
                           DO;                                                  
                              CALL P032_FJERN_STATUS                ;           
                                                                                
                              IF   FEIL_MELD_NR      >   0          THEN        
                                   GO   TO   RETUR;                             
                                                                                
                              CALL P010_FJERN_RESTEN                ;           
                           END;                                                 
                                                                                
                        IF FEIL_MELD_NR             >   0           THEN        
                           GO   TO   RETUR;                                     
                     END;                                                       
                                                                                
                  W001_DLI.GET       = 'GN  ';                                  
                                                                                
               END;  /*    DO WHILE  (  P036_LEST_TILKN_PCB2 )      */          
                                                                                
               IF FEIL_MELD_NR          >  0      THEN                          
                                                                                
                  /* ****************************************** */              
                  /* FEIL VED LESING AV    :      TILKN         */              
                  /* ****************************************** */              
                                                                                
                  GO TO  RETUR;                                                 
                                                                                
               SSA3_RF0PERSN.PKEY          =  W01_TILKN1.FNR_TILKN;             
               CALL P032_FJERN_STATUS;                                          
                                                                                
               IF FEIL_MELD_NR             >  0         THEN                    
                  GO   TO   RETUR;                                              
                                                                                
               CALL P010_FJERN_RESTEN                   ;                       
                                                                                
               IF   FEIL_MELD_NR           >  0         THEN                    
                    GO   TO   RETUR;                                            
            END;                                                                
         ELSE                                                                   
            /* *************************************************** */           
            /* TILKN_KODE = D ): DØD PERSN. SJEKKER OM FLERE TILKN.*/           
            /* ER DET FJERNES BARE TILKN . HVIS IKKE TAS STATUS   .*/           
            /* *************************************************** */           
            DO;                                                                 
               SSA1_TILKN.REL_OP           =  '^='               ;              
               SSA1_TILKN.PKEY             =  SSA1_RF0PERSN.PKEY ;              
               SSA3_RF0PERSN.PKEY          =  W01_TILKN1.FNR_TILKN;             
                                                                                
               IF P037_FLERE_TILKN                   THEN                       
                  DO;                                                           
                     SSA1_TILKN.REL_OP     =  ' ='               ;              
                     CALL P038_FJERN_TILKN                       ;              
                  END;                                                          
               ELSE                                                             
                  DO;                                                           
                     CALL P032_FJERN_STATUS                      ;              
                                                                                
                     IF   FEIL_MELD_NR      >  0              THEN              
                          GO   TO   RETUR;                                      
                                                                                
                     CALL P010_FJERN_RESTEN                      ;              
                  END;                                                          
                                                                                
               IF FEIL_MELD_NR      >  0                      THEN              
                  GO   TO   RETUR;                                              
            END;                                                                
                                                                                
         W001_DLI.GET                      =  'GN  '             ;              
                                                                                
      END;  /*   DO  WHILE          ( P034_LEST_TILKN_PCB1 )   */               
                                                                                
                                                                                
      IF FEIL_MELD_NR          >  0      THEN                                   
         DO;                                                                    
            /* ****************************************** */                    
            /* FEIL VED LESING AV    :      TILKN         */                    
            /* ****************************************** */                    
                                                                                
            GO TO  RETUR;                                                       
         END;                                                                   
                                                                                
      SSA3_RF0PERSN.PKEY       =     SSA1_RF0PERSN.PKEY    ;                    
      CALL P032_FJERN_STATUS                               ;                    
                                                                                
      IF   FEIL_MELD_NR        >     0                     THEN                 
           GO   TO   RETUR;                                                     
                                                                                
      CALL P010_FJERN_RESTEN                               ;                    
                                                                                
      IF   FEIL_MELD_NR        >     0             THEN                         
           GO   TO   RETUR;                                                     
                                                                                
  RETUR:                                                                        
    END P030_LEST_TILKN_FJERNET_ALT;                                            
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å FJERNE    :   STATUS                           */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P032_FJERN_STATUS:                                                          
      PROC;                                                                     
                                                                                
                                                                                
      CALL PLITDLI                              (W001_DLI.PARM_CT_5,            
                                                 W001_DLI.GHU,                  
                                                 RF3,                           
                                                 W001_IO,                       
                                                 SSA3_RF0PERSN,                 
                                                 SSA1_STATUS);                  
                                                                                
      IF  UIBFCTR                          ^=    UIB_RC_OK  THEN                
        DO;                                                                     
          /* ******************************************************** */        
          /*  UAKSEPTABEL KODE FRA  :   UIB                           */        
          /* ******************************************************** */        
  L200:                                                                         
          FEIL_MELD_NR                      =    500;                           
          FEIL_VED_LABEL                    =   '200';                          
          GO   TO  RETUR;                                                       
        END;                                                                    
                                                                                
      IF RF3.STATUS_KODE                    =    '  '             THEN          
        DO;                                                                     
          CALL PLITDLI                          (W001_DLI.PARM_CT_3,            
                                                 W001_DLI.DLET,                 
                                                 RF3,                           
                                                 W001_IO);                      
                                                                                
          IF  RF3.STATUS_KODE              ^=    '  '                           
            ! UIBFCTR                      ^=   UIB_RC_OK  THEN                 
            DO;                                                                 
            /* **************************************************** */          
            /*  UAKSEPTABEL KODE FRA  :   UIB                       */          
            /*                            RF3.STATUS_KODE           */          
            /* **************************************************** */          
  L210:                                                                         
            FEIL_MELD_NR              =   500;                                  
            FEIL_VED_LABEL            =  '210';                                 
            DB_STATUS_KODE            =   RF3.STATUS_KODE;                      
            GO TO     RETUR;                                                    
            END;                                                                
        END;                                                                    
                                                                                
      ELSE                                                                      
        IF RF3.STATUS_KODE                 ^=    'GE'             THEN          
          DO;                                                                   
            /* **************************************************** */          
            /*  UAKSEPTABEL KODE FRA  :   RF3.STATUS_KODE           */          
            /* **************************************************** */          
  L220:                                                                         
            FEIL_MELD_NR              =   500;                                  
            FEIL_VED_LABEL            =  '220';                                 
            DB_STATUS_KODE            =   RF3.STATUS_KODE;                      
            GO TO     RETUR;                                                    
          END;                                                                  
                                                                                
  RETUR:                                                                        
    END P032_FJERN_STATUS;                                                      
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å LESE TILKNYTTNINGENE TIL STATUS-SEGM.          */        
    /*                                                                */        
    /* PROCEDUREN RETURNERER MED VERDIEN SANN (1) / USANN (0) .       */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P034_LEST_TILKN_PCB1 :                                                      
      PROC           RETURNS (BIT(1));                                          
                                                                                
                                                                                
      DCL   W01_RETUR                            BIT   (1) INIT ('0'B);         
      SSA_UQUAL                             =   'TILKN    ';                    
                                                                                
      CALL PLITDLI                              (W001_DLI.PARM_CT_6,            
                                                 W001_DLI.GET,                  
                                                 RF1,                           
                                                 W01_TILKN1,                    
                                                 SSA1_RF0PERSN,                 
                                                 SSA1_STATUS,                   
                                                 SSA_UQUAL);                    
                                                                                
      IF UIBFCTR                           ^=    UIB_RC_OK        THEN          
         DO;                                                                    
            /* ************************************************** */            
            /* UAKSEPTABEL KODE FRA   :      UIB                  */            
            /* ************************************************** */            
  L230:                                                                         
            FEIL_MELD_NR                     =     500;                         
            FEIL_VED_LABEL                   =    '230';                        
         END;                                                                   
                                                                                
      SELECT  ( RF1.STATUS_KODE   );                                            
                                                                                
         WHEN ('  ' , 'GA' , 'GK' )                                             
            DO;                                                                 
             /* ************************************************ */             
             /* LEST INN EN   :   TILKN                          */             
            /* ************************************************* */             
                                                                                
            W01_RETUR                        =    '1'B;                         
         END;                                                                   
                                                                                
         WHEN ('GE' , 'GB')                                                     
            DO;                                                                 
              /* ********************************************* */               
              /* LOVLIG KODE MEN IKKE FUNNET NOEN   : TILKN    */               
              /* ********************************************* */               
                                                                                
              W01_RETUR                        =    '0'B;                       
         END;                                                                   
                                                                                
         OTHERWISE                                                              
            DO;                                                                 
               /* ************************************************* */          
               /* LEST INN EN   :   TILKN                           */          
               /* ************************************************* */          
                                                                                
  L240:                                                                         
               FEIL_MELD_NR                     =     500;                      
               FEIL_VED_LABEL                   =    '240';                     
               DB_STATUS_KODE                   =     RF1.STATUS_KODE;          
               W01_RETUR                        =     '0'B;                     
            END;                                                                
      END;     /*   SELECT (RF1.STATUS_KODE )                   */              
                                                                                
  RETUR:                                                                        
      RETURN (W01_RETUR);                                                       
    END P034_LEST_TILKN_PCB1;                                                   
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å LESE TILKNYTTNINGENE TIL STATUS-SEGM.          */        
    /*                                                                */        
    /* PROCEDUREN RETURNERER MED VERDIEN SANN (1) / USANN (0) .       */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P036_LEST_TILKN_PCB2 :                                                      
      PROC           RETURNS (BIT(1));                                          
                                                                                
                                                                                
      DCL   W01_RETUR                            BIT   (1) INIT ('0'B);         
      SSA_UQUAL                             =   'TILKN    ';                    
                                                                                
      CALL PLITDLI                              (W001_DLI.PARM_CT_6,            
                                                 W001_DLI.GET,                  
                                                 RF2,                           
                                                 W01_TILKN2,                    
                                                 SSA2_RF0PERSN,                 
                                                 SSA1_STATUS,                   
                                                 SSA_UQUAL);                    
                                                                                
      IF   UIBFCTR                         ^=    UIB_RC_OK        THEN          
        DO;                                                                     
           /* ******************************************************* */        
           /* UAKSEPTABEL KODE FRA   :      UIB                       */        
           /* ******************************************************* */        
  L250:                                                                         
           FEIL_MELD_NR                     =     500;                          
           FEIL_VED_LABEL                   =    '250';                         
        END;                                                                    
                                                                                
      SELECT   ( RF2.STATUS_KODE );                                             
         WHEN  ( '  ' , 'GA' , 'GK' )                                           
            DO;                                                                 
               /* ************************************************** */         
               /* LEST INN EN  :    TILKN                            */         
               /* ************************************************** */         
                                                                                
               W01_RETUR                        =    '1'B;                      
            END;                                                                
                                                                                
         WHEN  ( 'GE' , 'GB' )                                                  
            DO;                                                                 
               /* ************************************************** */         
               /* FINNES IKKE  :    TILKN                            */         
               /* ************************************************** */         
                                                                                
               W01_RETUR                        =    '0'B;                      
            END;                                                                
                                                                                
         OTHERWISE                                                              
            DO;                                                                 
               /* ************************************************* */          
               /* UAKSEPTABEL KODE FRA   :      RF1.STATUS_KODE     */          
               /* ************************************************* */          
  L260:                                                                         
               FEIL_MELD_NR                     =     500;                      
               FEIL_VED_LABEL                   =    '260';                     
               DB_STATUS_KODE                   =     RF2.STATUS_KODE;          
               W01_RETUR                        =    '0'B;                      
            END;                                                                
      END;                                                                      
                                                                                
  RETUR:                                                                        
      RETURN (W01_RETUR);                                                       
    END P036_LEST_TILKN_PCB2;                                                   
                                                                                
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å FJERNE  : TILKN                                */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P037_FLERE_TILKN :                                                          
      PROC  RETURNS          ( BIT (1) );                                       
                                                                                
                                                                                
      DCL   W037_RETUR         BIT (1)     INIT ('0'B);                         
                                                                                
                                                                                
      CALL PLITDLI                        (W001_DLI.PARM_CT_6,                  
                                           W001_DLI.GHU,                        
                                           RF3,                                 
                                           W001_IO,                             
                                           SSA3_RF0PERSN,                       
                                           SSA1_STATUS,                         
                                           SSA1_TILKN);                         
                                                                                
      IF  DLIUIB.UIBRCODE.UIBFCTR ^=       UIB_RC_OK    THEN                    
        DO;                                                                     
          /* ******************************************** */                    
          /* FEIL VED DB                                  */                    
          /* ******************************************** */                    
  L270:                                                                         
          FEIL_MELD_NR             =       500 ;                                
          FEIL_VED_LABEL           =      '270';                                
          GO   TO  RETUR;                                                       
        END;                                                                    
                                                                                
                                                                                
      SELECT  ( RF3.STATUS_KODE );                                              
                                                                                
         WHEN ('  ')                                                            
              /* ******************************************** */                
              /* ET ANNET TILKN SEGM FUNNET .                 */                
              /* ******************************************** */                
                                                                                
              W037_RETUR    =   '1'B;                                           
                                                                                
         WHEN ('GE')                                                            
              /* ******************************************** */                
              /* DET FINNES IKKE FLERE TILKNYTTINGS-SEGM.     */                
              /* ******************************************** */                
                                                                                
              W037_RETUR    =   '0'B;                                           
                                                                                
         OTHERWISE                                                              
            DO;                                                                 
              /* ******************************************** */                
              /* FEIL VED LES   AV :  TILKN                   */                
              /* ******************************************** */                
  L280:                                                                         
              FEIL_MELD_NR             =       500 ;                            
              FEIL_VED_LABEL           =      '280';                            
              DB_STATUS_KODE           =       RF3.STATUS_KODE;                 
              GO   TO  RETUR;                                                   
            END;                                                                
                                                                                
      END;/*   SELECT                               */                          
                                                                                
                                                                                
  RETUR:                                                                        
      RETURN (W037_RETUR);                                                      
                                                                                
    END P037_FLERE_TILKN;                                                       
                                                                                
                                                                                
                                                                                
                                                                                
    %PAGE;                                                                      
    /* ************************************************************** */        
    /*                                                                */        
    /* PROCEDURE FOR Å FJERNE  : TILKN                                */        
    /*                                                                */        
    /* ************************************************************** */        
                                                                                
    P038_FJERN_TILKN :                                                          
      PROC ;                                                                    
                                                                                
                                                                                
      CALL PLITDLI                        (W001_DLI.PARM_CT_6,                  
                                           W001_DLI.GHU,                        
                                           RF3,                                 
                                           W001_IO,                             
                                           SSA3_RF0PERSN,                       
                                           SSA1_STATUS,                         
                                           SSA1_TILKN);                         
                                                                                
      IF  DLIUIB.UIBRCODE.UIBFCTR ^=       UIB_RC_OK    THEN                    
        DO;                                                                     
          /* ******************************************** */                    
          /* FEIL VED LES   AV :  TILKN                   */                    
          /* ******************************************** */                    
  L300:                                                                         
          FEIL_MELD_NR             =       500 ;                                
          FEIL_VED_LABEL           =      '300';                                
          GO   TO  RETUR;                                                       
        END;                                                                    
                                                                                
                                                                                
      SELECT  ( RF3.STATUS_KODE );                                              
                                                                                
         WHEN ('  ')                                                            
            DO;                                                                 
              /* ******************************************** */                
              /* TILKN      FUNNET  . SEGMENTET FJERNES .     */                
              /* ******************************************** */                
                                                                                
              CALL PLITDLI                   (W001_DLI.PARM_CT_3,               
                                              W001_DLI.DLET,                    
                                              RF3,                              
                                              W001_IO);                         
                                                                                
              IF  DLIUIB.UIBRCODE.UIBFCTR ^=  UIB_RC_OK                         
                ! RF3.STATUS_KODE         ^=  '  '         THEN                 
                DO;                                                             
                  /* ******************************************** */            
                  /* FEIL VED DELETE  AV :  TILKN                 */            
                  /* ******************************************** */            
  L310:                                                                         
                  FEIL_MELD_NR             =   500 ;                            
                  FEIL_VED_LABEL           =  '310';                            
                  DB_STATUS_KODE           =   RF3.STATUS_KODE;                 
                  GO   TO  RETUR;                                               
                END;                                                            
                                                                                
            END;                                                                
                                                                                
         WHEN ('GE')                                                            
            DO;                                                                 
              /* ******************************************** */                
              /* TILKN      IKKE FUNNET   FEIL                */                
              /* ******************************************** */                
  L320:                                                                         
              FEIL_MELD_NR             =       504 ;                            
              FEIL_VED_LABEL           =      '320';                            
              GO   TO  RETUR;                                                   
            END;                                                                
                                                                                
         OTHERWISE                                                              
            DO;                                                                 
              /* ******************************************** */                
              /* FEIL VED LES   AV :  TILKN                   */                
              /* ******************************************** */                
  L330:                                                                         
              FEIL_MELD_NR             =       500 ;                            
              FEIL_VED_LABEL           =      '330';                            
              DB_STATUS_KODE           =       RF3.STATUS_KODE;                 
              GO   TO  RETUR;                                                   
            END;                                                                
                                                                                
      END;/*   SELECT                               */                          
                                                                                
                                                                                
  RETUR:                                                                        
                                                                                
    END P038_FJERN_TILKN;                                                       
                                                                                
                                                                                
  L999:                                                                         
                                                                                
    EXEC CICS RETURN;                                                           
  END  R001550;                                                                 
